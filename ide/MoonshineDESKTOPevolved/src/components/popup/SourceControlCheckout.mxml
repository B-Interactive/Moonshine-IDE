<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
// Copyright 2016 Prominic.NET, Inc.
// 
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
// 
// http://www.apache.org/licenses/LICENSE-2.0 
// 
// Unless required by applicable law or agreed to in writing, software 
// distributed under the License is distributed on an "AS IS" BASIS, 
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and 
// limitations under the License
// 
// Author: Prominic.NET, Inc.
// No warranty of merchantability or fitness of any kind. 
// Use this software at your own risk.
////////////////////////////////////////////////////////////////////////////////
-->
<resizableTitleWindow:ResizableTitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009" 
										   xmlns:s="library://ns.adobe.com/flex/spark"
										   backgroundColor="0xf5f5f5"
										   width="500" minWidth="400" minHeight="100" autoLayout="true"
										   xmlns:view="actionScripts.plugin.findreplace.view.*" xmlns:resizableTitleWindow="actionScripts.ui.resizableTitleWindow.*"
										   skinClass="components.skins.ResizableTitleWindowSkin" creationComplete="onCreationCompletes()" xmlns:mx="library://ns.adobe.com/flex/mx">
	<fx:Metadata>
		[Event(name="name", type="flash.events.Event")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[
			import com.adobe.utils.StringUtil;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.validators.Validator;
			
			import spark.events.TextOperationEvent;
			
			import actionScripts.locator.IDEModel;
			import actionScripts.plugins.svn.event.SVNEvent;
			import actionScripts.plugins.versionControl.VersionControlUtils;
			import actionScripts.valueObjects.RepositoryItemVO;
			import actionScripts.valueObjects.VersionControlTypes;
			
			[Bindable] public var isGitAvailable:Boolean;
			[Bindable] public var type:String;
			
			public var editingRepository:RepositoryItemVO;
			public var submitObject:Object;
			public var isTrustServerCertificate:Boolean;
			
			[Bindable] private var isValidURL:Boolean;
			[Bindable] private var isPathMapped:Boolean;
			
			private var mappedPath:String
			private var isFolderNameManuallyChanged:Boolean;
			private var isFolderNameManuallyEmpty:Boolean;
			private var model:IDEModel = IDEModel.getInstance();
			
			public function notifySVNCheckoutError():void
			{
				Alert.show("Oops! There is something wrong!\nPlease check console for details.", "Error!");
				btnSubmit.enabled = true;
			}
			
			private function onCheckoutRequest():void
			{
				var tmpArr:Array = new Array(fnValidator);
				if (Validator.validateAll(tmpArr).length != 0)
				{
					return;
				}
				
				submitObject = new Object();
				submitObject.url = txtSource.text;
				submitObject.target = mappedPath;
				submitObject.targetFolder = txtFolderName.text;
				submitObject.repository = editingRepository;
				
				if (type != VersionControlTypes.SVN) dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
				else 
				{
					dispatchEvent(new SVNEvent(SVNEvent.EVENT_CHECKOUT, null));
					dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
				}
			}
			
			private function browse():void
			{
				model.fileCore.browseForDirectory("Select Parent Directory", onDirectorySelected);
			}
			
			private function onDirectorySelected(fileObj:Object):void
			{
				mappedPath = (fileObj as File).nativePath;
				
				VersionControlUtils.IS_CHECKOUT_BROWSED_ONCE = true;
				isPathMapped = true;
				validateURLInput();
			}
			
			private function validateURLInput():void
			{
				var autoFolderName:String;
				var searchRegExp:RegExp;
				var results:Array;
				if (type == VersionControlTypes.GIT)
				{
					// expected format is https://github.com/{organization/author}/{project}
					searchRegExp = new RegExp("(?:https?|git|ssh)\://.*/.*/([^/]+)(?:\\.git)?", "i");
					results = searchRegExp.exec(txtSource.text);
					if (results != null)
					{
						isValidURL = true;
						txtTarget.text = (isPathMapped) ? mappedPath : "";
						if (!isPathMapped) txtTarget.prompt = "Parent directory";
						moveCursorToTargetEnd();
						autoFolderName = results[0].split("/").pop();
						if (!isFolderNameManuallyChanged || isFolderNameManuallyEmpty)
						{
							txtFolderName.text = autoFolderName;
							isFolderNameManuallyChanged = isFolderNameManuallyEmpty = false;
						}
						
						return;
					}
				}
				else
				{
					searchRegExp = new RegExp(".*://.*/.*.$", "i");
					results = searchRegExp.exec(txtSource.text);
					if (results != null)
					{
						isGitAvailable = isValidURL = true;
						txtTarget.text = (isPathMapped) ? mappedPath : "";
						if (!isPathMapped) txtTarget.prompt = "Parent directory";
						moveCursorToTargetEnd();
						txtFolderName.text = (txtFolderName.text != "") ? txtFolderName.text : txtSource.text.split("/").pop();
						
						return;
					}
				}
				
				if (!results && !isFolderNameManuallyChanged) txtFolderName.text = "";
				isValidURL = false;
			}
			
			protected function onCreationCompletes():void
			{
				if (editingRepository) 
				{
					txtSource.text = editingRepository.url;
				}
				if (VersionControlUtils.IS_CHECKOUT_BROWSED_ONCE)
				{
					mappedPath = model.fileCore.nativePath;
					isPathMapped = true;
				}
				
				lblSeparator.text = File.separator;
				txtSource.setFocus();
				validateURLInput();
			}
			
			protected function onFolderNameChanged(event:TextOperationEvent):void
			{
				if (StringUtil.trim(txtFolderName.text).length == 0)
				{
					isValidURL = false;
					isFolderNameManuallyEmpty = true;
					isFolderNameManuallyChanged = false;
				}
				else
				{
					isFolderNameManuallyEmpty = false;
					isFolderNameManuallyChanged = true;
					validateURLInput();
				}
			}
			
			private function moveCursorToTargetEnd():void
			{
				if (isPathMapped)
				{
					// this will move the cursor to the end
					txtTarget.appendText('');
				}
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<mx:StringValidator id="fnValidator" source="{txtFolderName}" property="text" trigger="{btnSubmit}" triggerEvent="click"/>
		<mx:StringValidator id="eValidator" source="{txtLogin}" property="text" trigger="{btnSubmit}" triggerEvent="click"/>
		<mx:StringValidator id="pValidator" source="{txtPassword}" property="text" trigger="{btnSubmit}" triggerEvent="click"/>
	</fx:Declarations>
	
	<s:VGroup height="100%" width="100%" 
			  paddingLeft="12" paddingBottom="12" paddingTop="9" paddingRight="12">
		<view:PromptTextInput id="txtSource"
							  width="100%" styleName="textInput"
							  prompt="Repository URL" change="validateURLInput()"/>
		<s:HGroup verticalAlign="middle" width="100%">
			<view:PromptTextInput id="txtTarget" 
								  prompt="Where to download" width="60%" styleName="textInput"
								  tabIndex="2" editable="false"/>
			<s:Button icon="@Embed('/elements/images/icoTreeFolder_close.png')"
					  styleName="lightButton" toolTip="Choose Parent Directory"
					  click="browse()"/>
			<s:Label id="lblSeparator"/>
			<view:PromptTextInput id="txtFolderName"
								  prompt="Target folder name" width="40%"
								  styleName="textInput" restrict="0-9A-Za-z_"
								  change="onFolderNameChanged(event)"/>
		</s:HGroup>
		<s:Label text="You need Git installed on your system to continue."
				 width="100%" textAlign="center" color="0xff0000"
				 visible="{!isGitAvailable &amp;&amp; type == VersionControlTypes.GIT}" includeInLayout="{!isGitAvailable &amp;&amp; type == VersionControlTypes.GIT}"/>
		<s:HGroup width="100%" verticalAlign="middle"
				  includeInLayout="false" visible="false">
			<s:CheckBox id="cbLogin"
						label="Need Authentication" selected="false"/>
			<s:CheckBox label="Trust Server Certificate"
						change="isTrustServerCertificate=event.target.selected"/>
		</s:HGroup>
		<s:VGroup width="100%"
				  visible="{cbLogin.selected &amp;&amp; type == VersionControlTypes.SVN}" includeInLayout="{cbLogin.selected &amp;&amp; type == VersionControlTypes.SVN}">
			<view:PromptTextInput
				id="txtLogin" 
				prompt="User Name"
				width="100%"
				styleName="textInput"/>
			<view:PromptTextInput
				id="txtPassword" 
				prompt="Password" change="txtPassword.displayAsPassword=true"
				width="100%"
				styleName="textInput"/>
		</s:VGroup>
	</s:VGroup>
	
	<resizableTitleWindow:controlBarContent>
		<s:Spacer width="100%"/>
		<s:Button id="btnSubmit" 
				  label="{(type == VersionControlTypes.GIT) ? 'Pull' : 'Checkout'}" styleName="darkButton" enabled="{isGitAvailable &amp;&amp; isValidURL &amp;&amp; isPathMapped}"
				  click="onCheckoutRequest()"/>
	</resizableTitleWindow:controlBarContent>
</resizableTitleWindow:ResizableTitleWindow>
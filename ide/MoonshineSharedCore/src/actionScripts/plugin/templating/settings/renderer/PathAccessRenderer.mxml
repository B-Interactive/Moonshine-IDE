<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
//  Copyright (C) STARTcloud, Inc. 2015-2022. All rights reserved.
//
//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the Server Side Public License, version 1,
//  as published by MongoDB, Inc.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  Server Side Public License for more details.
//
//  You should have received a copy of the Server Side Public License
//  along with this program. If not, see
//
//  http://www.mongodb.com/licensing/server-side-public-license
//
//  As a special exception, the copyright holders give permission to link the
//  code of portions of this program with the OpenSSL library under certain
//  conditions as described in each individual source file and distribute
//  linked combinations including the program with the OpenSSL library. You
//  must comply with the Server Side Public License in all respects for
//  all of the code used other than as permitted herein. If you modify file(s)
//  with this exception, you may extend this exception to your version of the
//  file(s), but you are not obligated to do so. If you do not wish to do so,
//  delete this exception statement from your version. If you delete this
//  exception statement from all source files in the program, then also delete
//  it in the license file.
//
////////////////////////////////////////////////////////////////////////////////
-->
<s:VGroup 
	xmlns:fx="http://ns.adobe.com/mxml/2009"
  	xmlns:s="library://ns.adobe.com/flex/spark"
	width="100%"
	paddingLeft="15"
	paddingTop="5"
	paddingRight="15"
	paddingBottom="5"
	visible="true"
	creationComplete="init()"
	resize="onResizeEvent(event)">
	
	<fx:Script>
		<![CDATA[
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.ResizeEvent;
			import mx.events.ToolTipEvent;
			
			import actionScripts.factory.FileLocation;
			import actionScripts.locator.IDEModel;
			import actionScripts.plugin.templating.settings.PathAccessSetting;
			import actionScripts.utils.UtilsCore;
			
			public static const EVENT_REFRESH:String = "refresh";
			
			[Bindable] public var setting:PathAccessSetting;
			
			private var path:String = "";
			private var model:IDEModel = IDEModel.getInstance();
			
			private function init():void
			{
				imgError.addEventListener(ToolTipEvent.TOOL_TIP_CREATE, UtilsCore.createCustomToolTip, false, 0, true);
				imgError.addEventListener(ToolTipEvent.TOOL_TIP_SHOW, UtilsCore.positionTip, false, 0, true);
				
				tripPathValue();
			}
			
			private function tripPathValue():void
			{
				// we shall show relative paths for those who exists.
				// setting.project will only be true if the path is exist.
				// therefore we'll compare the path with setting.project
				if (setting.originalPath.fileBridge.exists) path = setting.project.folderLocation.fileBridge.getRelativePath(setting.originalPath, true);
				else path = setting.originalPath.fileBridge.nativePath;
				
				// even if the path comes blank, in case of project.getRelativePath(project)
				if (path == "") path = setting.originalPath.fileBridge.nativePath;
				
				onResizeEvent(null);
			}
			
			protected function onAddPermission(event:MouseEvent):void
			{
				var tmpStartLocation:FileLocation = setting.originalPath;
				if (setting.isLocalePath)
				{
					var tmpPathArr:Array = setting.originalPath.fileBridge.nativePath.split(setting.originalPath.fileBridge.separator);
					tmpPathArr.splice(tmpPathArr.length - 1, 1);
					var tmpLocaleLocation:FileLocation = new FileLocation(tmpPathArr.join(setting.originalPath.fileBridge.separator));
					if (tmpLocaleLocation.fileBridge.exists) tmpStartLocation = tmpLocaleLocation;
				}
				
				CONFIG::OSX
				{
					model.fileCore.browseForDirectory("Select Directory", openNewAccess, newAccessCancelled, tmpStartLocation.fileBridge.nativePath);
					return;
				}
				
				// in @development cases
				if (!tmpStartLocation.fileBridge.exists)
				{
					Alert.yesLabel = "File";
					Alert.cancelLabel = "Folder";
					Alert.show("Please choose selection type", "Type Selection", Alert.YES | Alert.CANCEL, null, onTypeSelected, null, 0);
				}
				else
				{
					if (tmpStartLocation.fileBridge.isDirectory || setting.isLocalePath) model.fileCore.browseForDirectory("Select Directory", openNewAccess, newAccessCancelled, tmpStartLocation.fileBridge.nativePath);
					else model.fileCore.browseForOpen("Select File", openNewAccess, newAccessCancelled, null, tmpStartLocation.fileBridge.nativePath);
				}
				
				/*
				 *@local
				 */
				function onTypeSelected(event:CloseEvent):void
				{
					Alert.yesLabel = "Yes";
					Alert.cancelLabel = "Cancel";
					
					if (event.detail == Alert.CANCEL) model.fileCore.browseForDirectory("Select Directory", openNewAccess, newAccessCancelled, tmpStartLocation.fileBridge.nativePath);
					else model.fileCore.browseForOpen("Select File", openNewAccess, newAccessCancelled, null, tmpStartLocation.fileBridge.nativePath);
				}
			}
			
			protected function openNewAccess(fileDir:Object):void
			{
				// jhar khachhe at FlashBuilderExporter.export in defineFolderAccess.mxml - jokon file path has /{locale}
				// same thing chk korar somoy with non-{locale}, file chooser khulche file selection mode e - eta hochhe jokon file.exists = false, ebong
				// file.isdirectory always coming false at that time, in onAddPermission() method above
				
				var finalPath: String = fileDir.nativePath;
				if (setting.isLocalePath) finalPath += setting.originalPath.fileBridge.separator +"{locale}";
				
				setting.originalPath.fileBridge.nativePath = finalPath;
				dispatchEvent(new Event(EVENT_REFRESH));
			}
			
			protected function newAccessCancelled():void
			{
			}
			
			protected function onResizeEvent(event:ResizeEvent):void
			{
				var thisWidthChar:int = Math.floor(width / 8);
				var availableWidthByChar:int = thisWidthChar - 13; // 100/8
				if (path.length > availableWidthByChar)
				{
					var lastPart:String = path.slice(path.length - (availableWidthByChar - 4));
					updatePathWithValue("...."+ lastPart);	
				}
				else
				{
					updatePathWithValue(path);
				}
				
				/*
				 * @local
				 */
				function updatePathWithValue(value:String):void
				{
					txtPath.callLater(function():void
					{
						txtPath.text = value;
					});	
				}
			}
			
		]]>
	</fx:Script>
	
	<s:HGroup width="100%" verticalAlign="middle">
		<s:Label id="txtPath" 
				 styleName="uiTextSettingsValue" width="100%"
				 paddingRight="20"/>
		<s:Image id="imgError" 
				 source="@Embed('/elements/images/iconExclamationRed.png')"
				 toolTip="{setting.errorType}"/>
		<s:Button label="Add" styleName="lightButton"
				  click="onAddPermission(event)"/>
	</s:HGroup>
	
</s:VGroup>